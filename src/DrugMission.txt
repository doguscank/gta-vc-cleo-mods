{$CLEO .cs} // Directive to compile as a CLEO script

// -1155.3096 -411.5808 10.9455 (1st pickup)
// -1049.1829 306.7443 11.263 (2nd pickup)

:start
0000: NOP // NOP is used in order to avoid jump-at-zero offset error

:player_loop
0001: wait 0 ms
00D6: if and
0256:   player $PLAYER_CHAR defined
00F5: player $PLAYER_CHAR 0 -395.1632 -581.6179 19.5742 radius 20.0 20.0 4.0
004D: jf @player_loop
0002: jump @request_models

:request_models
0001: wait 0 ms
0247: request_model #BRIEFCASE // briefcase pickup
0247: request_model #BMYBB // dealer ped
0247: request_model #CBA // cuban gang1 ped
0247: request_model #CBB // cuban gang2 ped
0247: request_model #UZI
0247: request_model #RUGER
038B: load_requested_models
0002: jump @check_models

:check_models
00D6: if and
0248:   model #BRIEFCASE available
0248: model #BMYBB available
004D: jf @request_models
0002: jump @create_dealer

:create_dealer
0001: wait 0 ms
// -395.1842 -582.9283 19.5742 dealer coords 0.0 dealer angle
009A: 0@ = create_actor_pedtype 4 model #BMYBB at -395.1526 -583.0482 19.5742
0173: set_actor 0@ z_angle_to 0.0
0001: wait 10 ms
0350: set_actor 0@ maintain_position_when_attacked 1
0002: jump @create_sphere

:create_sphere
03BC: 1@ = create_sphere -395.1632 -581.2679 19.5742 0.75
0249: release_model #BMYBB
0002: jump @check_sphere

:check_dealer_dead
0001: wait 0 ms
00D6: if
0118:   actor 0@ dead
004D: jf @check_sphere
0350: set_actor 0@ maintain_position_when_attacked 0
03BD: destroy_sphere 1@
0001: wait 15000 ms
0002: jump @request_models

:check_sphere
0001: wait 0 ms
00D6: if and
00F5:   player $PLAYER_CHAR 0 -395.1632 -581.6179 19.5742 radius 1.0 1.0 2.0
0038:   $ONMISSION == 0
004D: jf @check_dealer_dead
0002: jump @start_mission

:start_mission
0001: wait 0 ms
0004: $ONMISSION = 1
0180: set_on_mission_flag_to $ONMISSION
03BD: destroy_sphere 1@
0002: jump @start_mission_cutscene

:start_mission_cutscene
01B4: set_player $PLAYER_CHAR can_move 0
0210: player $PLAYER_CHAR look_at_actor 0@
022D: set_actor 0@ to_look_at_player $PLAYER_CHAR
0373: set_camera_directly_behind_player
02A3: enable_widescreen 1
00BA: text_styled 'DM1' 15000 ms 2
0673: play_animation on actor 0@ animgroup 0 anim 11 blendfactor 50.0
00BB: text_lowpriority 'DM1_D1' time 3000 1
00BB: text_lowpriority 'DM1_D2' time 3000 1
00BB: text_lowpriority 'DM1_T1' time 3000 1
00BB: text_lowpriority 'DM1_D3' time 3000 1
00BB: text_lowpriority 'DM1_T2' time 3000 1
0001: wait 15000 ms
0372: set_actor 0@ anim 19 wait_state_time -1 ms
022F: set_actor 0@ stop_looking
02A3: enable_widescreen 0
01B4: set_player $PLAYER_CHAR can_move 1
0002: jump @create_pickup_1

:create_pickup_1
0001: wait 0 ms
00BB: text_lowpriority 'DM1_X1' time 10000 1
0213: 2@ = create_pickup #BRIEFCASE type 3 at -1155.3096 -411.5808 10.9455
018A: 3@ = create_checkpoint_at -1155.3096 -411.5808 10.9455
//04BB: select_interiour 0 // select render area
//0055: put_player $PLAYER_CHAR at -1155.3096 -411.5808 20.9455
0002: jump @check_player_close_1

:check_player_close_1
0001: wait 0 ms
00D6: if
00F5:   player $PLAYER_CHAR 0 -367.4766 -535.7837 17.282 radius 20.0 20.0 4.0
004D: jf @check_player_close_1
0002: jump @create_gangs_1

// 1st pickup gangs
// -1141.2717 -414.6779 10.9455 43.6096
// -1148.2882 -417.0597 10.9455 11.9626
// -1145.1401 -407.1232 10.9455 139.1536
// -1154.8859 -407.4587 10.9455 238.7946
// -1158.4279 -404.1169 10.9455 297.0749
// -1141.7587 -402.4537 10.9455 60.2162
// -1141.7587 -402.4537 10.9455 274.2015 (talk1)
// -1148.5876 -394.2104 10.9455 93.4299 (talk1)
// -1153.1554 -402.9647 10.9455 55.8295 (talk2)
// -1155.0703 -402.0601 10.9455 244.1446 (talk2)
// -1146.9677 -392.0272 10.9455 9.1656 (guard1)
// -1154.6221 -392.8821 10.9248 8.2257 (guard2)

:create_gangs_1
// DM_G1_1 = DrugMission Group1 Ped1
// $DM_G1_B1 = DrugMission Group1 Ped1 Blip
0005: $DM_G1_C1 = -1141.2717
0005: $DM_G1_C2 = -414.6779
0005: $DM_G1_C3 = 10.9455
009A: $DM_G1 = create_actor_pedtype 4 model #CBA at $DM_G1_C1 $DM_G1_C2 $DM_G1_C3
01B2: give_actor $DM_G1 weapon 23 ammo 9999 // Load the weapon model before using this
0223: set_actor $DM_G1 health_to 200
0173: set_actor $DM_G1 z_angle_to 43.6096
02E2: set_actor $DM_G1 weapon_accuracy_to 60
01CC: actor $DM_G1 kill_player $PLAYER_CHAR
0162: $DM_G1_M = create_marker_above_actor $DM_G1 color 4 display 2
0084: $DM_G1_1 = $DM_G1

:check_pickup_1
0001: wait 0 ms
00D6: if
0214:   pickup 2@ picked_up
004D: jf @check_pickup_1
0002: jump @remove_pickup_1

:remove_pickup_1
0001: wait 0 ms
0215: destroy_pickup 2@
0164: disable_marker 3@
0002: jump @create_pickup_2

:create_pickup_2
0001: wait 0 ms
03BD: destroy_sphere 1@
// -385.2228 -579.6104 25.3226 pickup coords
0213: 2@ = create_pickup #BRIEFCASE type 3 at -1049.1829 306.7443 11.263
018A: 3@ = create_checkpoint_at -1049.1829 306.7443 11.263
0002: jump @check_pickup_2

:check_pickup_2
0001: wait 0 ms
00D6: if
0214:   pickup 2@ picked_up
004D: jf @check_pickup_2
0002: jump @remove_pickup_2

:remove_pickup_2
0001: wait 0 ms
0215: destroy_pickup 2@
0164: disable_marker 3@

:deliver_drugs
0001: wait 0 ms
00BB: text_lowpriority 'DM1_X2' time 10000 1
03BC: 1@ = create_sphere -395.1632 -581.2679 19.5742 0.75
018A: 3@ = create_checkpoint_at -395.1632 -581.2679 19.5742

:check_deliver_drugs
0001: wait 0 ms
00D6: if
00F5:   player $PLAYER_CHAR 0 -395.1632 -581.6179 19.5742 radius 1.0 1.0 2.0
004D: jf @check_deliver_drugs
0002: jump @finish_mission_cutscene

:finish_mission_cutscene
0001: wait 0 ms
01B4: set_player $PLAYER_CHAR can_move 0
022D: set_actor 0@ to_look_at_player $PLAYER_CHAR
0210: player $PLAYER_CHAR look_at_actor 0@
0373: set_camera_directly_behind_player
02A3: enable_widescreen 1
0673: play_animation on actor 0@ animgroup 0 anim 171 blendfactor 50.0
00BB: text_lowpriority 'DM1_D4' time 4000 1
0001: wait 4000
0372: set_actor 0@ anim 19 wait_state_time -1 ms
022F: set_actor 0@ stop_looking
02A3: enable_widescreen 0
01B4: set_player $PLAYER_CHAR can_move 1
0002: jump @finish_mission

:finish_mission
0001: wait 0 ms
0394: play_music 1
01E3: text_1number_styled 'M_PASS' number 5000 time 5000 style 1
0109: player $PLAYER_CHAR money += 5000
0004: $ONMISSION = 0
0180: set_on_mission_flag_to $ONMISSION
0249: release_model #BRIEFCASE
0249: release_model #CBA
0249: release_model #CBB
0001: wait 15000 ms
00D6: if
0118:   actor 0@ dead
004D: jf @create_sphere
0002: jump @player_loop

0A93: end_custom_thread
