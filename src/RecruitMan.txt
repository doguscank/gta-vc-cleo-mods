{$CLEO .cs} // Directive to compile as a CLEO script

:start
0000: NOP // NOP is used in order to avoid jump-at-zero offset error

:player_loop
0001: wait 0 ms
00D6: if and
0256:   player $PLAYER_CHAR defined
00F5: player $PLAYER_CHAR 0 -367.4766 -535.7837 17.282 radius 20.0 20.0 4.0
004D: jf @player_loop
0002: jump @load_models

:load_models
0001: wait 0 ms
023C: load_special_actor 8 'IGBUDDY' // 8 is Lance Vance
0247: request_model #M60
038B: load_requested_models 
0002: jump @check_models

:check_models
0001: wait 0 ms
00D6: if and
023D:   special_actor 8 loaded
0248:   model #M60 available
004D: jf @load_models
0002: jump @spawn_recruit

:spawn_recruit
0001: wait 0 ms
009A: 0@ = create_actor_pedtype 4 model #SPECIAL08 at -367.4766 -535.7837 17.282
0173: set_actor 0@ z_angle_to 180.0
// M60 weapon id = 32
01B2: give_actor 0@ weapon 32 ammo 9999 // Load the weapon model before using this
0223: set_actor 0@ health_to 1000
02E2: set_actor 0@ weapon_accuracy_to 70
03BC: 1@ = create_sphere -367.4766 -537.2837 17.282 0.75
0296: unload_special_actor 8
0249: release_model #M60
0002: jump @sphere_check

:sphere_check
0001: wait 0 ms
00D6: if and
00F5:   player $PLAYER_CHAR 0 -367.4766 -537.2837 17.282 radius 1.0 1.0 2.0
010A: player $PLAYER_CHAR money > 4999
004D: jf @sphere_check
0002: jump @buy_recruit

:buy_recruit
0001: wait 0 ms
0109: player $PLAYER_CHAR money += -5000
01DF: tie_actor 0@ to_player $PLAYER_CHAR
03BD: destroy_sphere 1@
0002: jump @check_recruit_dead

:check_recruit_dead
0001: wait 0 ms
00D6: if
0118:   actor 0@ dead
004D: jf @check_recruit_group
0002: jump @remove_recruit

:check_recruit_group
0001: wait 0 ms
00D6: if
8320:   not actor 0@ in_range_of_player $PLAYER_CHAR
004D: jf @check_recruit_dead
0002: jump @remove_recruit

:remove_recruit
0001: wait 0 ms
01C2: mark_actor_as_no_longer_needed 0@
0002: jump @player_loop
